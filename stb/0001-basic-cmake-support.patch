From fb1a2f30b9a0b3d11794f29275c20d19d0790805 Mon Sep 17 00:00:00 2001
From: Hisanari Otsu <hi2p.perim@gmail.com>
Date: Tue, 14 May 2019 05:36:19 +0200
Subject: [PATCH] basic cmake support

---
 CMakeLists.txt           | 61 ++++++++++++++++++++++++++++++++++++++++
 cmake/stbConfig.cmake.in |  6 ++++
 2 files changed, 67 insertions(+)
 create mode 100644 CMakeLists.txt
 create mode 100644 cmake/stbConfig.cmake.in

diff --git a/CMakeLists.txt b/CMakeLists.txt
new file mode 100644
index 0000000..d30a609
--- /dev/null
+++ b/CMakeLists.txt
@@ -0,0 +1,61 @@
+cmake_minimum_required(VERSION 3.10)
+cmake_policy(SET CMP0048 NEW)
+project(stb VERSION 2.34.0)
+include(GNUInstallDirs)
+
+# -----------------------------------------------------------------------------
+
+add_library(stb_image INTERFACE)
+add_library(stb::stb_image ALIAS stb_image)
+target_include_directories(stb_image INTERFACE
+    "$<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}>"
+    "$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>")
+install(TARGETS stb_image EXPORT stbTargets)
+
+# -----------------------------------------------------------------------------
+
+# Install
+
+# Install include directory
+file(GLOB STB_SOURCES "*.h" "*.c")
+install(FILES ${STB_SOURCES} DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/stb")
+
+# Package configuration directory
+set(STB_PACKAGE_CONFIG_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME}")
+
+# Make package configuration files
+# Use configure_package_config_file to make the package relocatable
+include(CMakePackageConfigHelpers)
+# Generate *Config.cmake file
+configure_package_config_file(
+    "${CMAKE_CURRENT_LIST_DIR}/cmake/${PROJECT_NAME}Config.cmake.in"
+    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake"
+    INSTALL_DESTINATION ${STB_PACKAGE_CONFIG_DIR}
+)
+# Generate *ConfigVersion.cmake file
+write_basic_package_version_file(
+    "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
+    VERSION ${PROJECT_VERSION}
+    COMPATIBILITY AnyNewerVersion
+)
+# Install generated configuration files
+install(
+    FILES "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Config.cmake"
+          "${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}ConfigVersion.cmake"
+    DESTINATION ${STB_PACKAGE_CONFIG_DIR}
+)
+
+# Export targets
+# Make export target group
+install(
+    EXPORT ${PROJECT_NAME}Targets
+    FILE ${PROJECT_NAME}Targets.cmake
+    NAMESPACE ${PROJECT_NAME}::
+    DESTINATION ${STB_PACKAGE_CONFIG_DIR}
+)
+# Generate *Target.cmake file
+export(
+    EXPORT ${PROJECT_NAME}Targets
+    FILE ${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}Targets.cmake
+    NAMESPACE ${PROJECT_NAME}::
+)
diff --git a/cmake/stbConfig.cmake.in b/cmake/stbConfig.cmake.in
new file mode 100644
index 0000000..2b3abda
--- /dev/null
+++ b/cmake/stbConfig.cmake.in
@@ -0,0 +1,6 @@
+@PACKAGE_INIT@
+
+# Avoid to include the target if it is already loaded.
+if (NOT TARGET stb::stb_image)
+    include(${CMAKE_CURRENT_LIST_DIR}/stbTargets.cmake)
+endif()
-- 
2.20.0.windows.1

